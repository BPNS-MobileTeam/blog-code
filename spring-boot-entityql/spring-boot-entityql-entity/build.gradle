plugins {
    id 'pl.exsio.querydsl.entityql' version "0.0.12"
    id 'idea'
}

bootJar { enabled = false }
jar { enabled = true }

apply plugin: "io.spring.dependency-management"

dependencies {

    api('org.springframework.boot:spring-boot-starter-data-jpa')
    api "com.querydsl:querydsl-jpa"
    api "com.querydsl:querydsl-core"
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa" // querydsl JPAAnnotationProcessor 사용 지정
    annotationProcessor "jakarta.persistence:jakarta.persistence-api:2.2.3"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api:1.3.5"

    //DB
    implementation('com.h2database:h2')

    implementation("joda-time:joda-time:2.9.4") // querydsl-sql
    implementation("org.reflections:reflections:0.9.11") // entityql
    api("com.querydsl:querydsl-sql-spring:4.3.1") // querydsl-sql
    api("com.github.eXsio:querydsl-entityql:3.1.0") // entityql

}

def generatedSql='src/main/generated_sql'
def defaultPackage = 'com.woowabros.settler.domain.'
entityql {
    generators = [
            generator = {
                type = 'JPA'
                sourceClasses = [
                        defaultPackage+'billproof.eai.EaiBillProof',
                        defaultPackage+'txitem.TxDetail',
                        defaultPackage+'txitem.TxAddition',
                        defaultPackage+'txitem.TxItem',
                        defaultPackage+'txitem.daily.eai.TxItemSubKeySum',
                        defaultPackage+'txitem.daily.eai.TxDetailSubKeySum',
                        defaultPackage+'txitem.daily.eai.TxAdditionSubKeySum',
                        defaultPackage+'txitem.daily.TxItemSum',
                        defaultPackage+'txitem.daily.TxDetailSum',
                        defaultPackage+'txitem.daily.TxAdditionSum',
                ]
                destinationPackage = defaultPackage+'sql'
                destinationPath = file(generatedSql).absolutePath
                filenamePattern = 'E%s.java'
            }
    ]
    sourceSets.main.java.srcDirs += [generatedSql]
    idea.module.generatedSourceDirs += file(generatedSql)

}

clean.doLast {
    file(generatedSql).deleteDir()
}

// compileJava 실행시 SQL QClass 생성 자동 연계
generateModels {
    dependsOn('compileJava')
}

// querydsl 적용
def generated='src/main/generated'
sourceSets.main.java.srcDirs += [generated]

tasks.withType(JavaCompile) {
    options.annotationProcessorGeneratedSourcesDirectory = file(generated)
}

clean.doLast {
    file(generated).deleteDir()
}
